##
# "Carousel" element
#
prototype(ISP.BasicStructure:Carousel) < prototype(Neos.Neos:ContentComponent) {
    identifier = ${'id-' + q(node).property('_identifier')}

    carouselItems = Neos.Neos:ContentCollection {
        nodePath = 'carouselitems'
        attributes.class = 'carousel-inner'

        // Each child item is augmented "item" and "active" class
        prototype(Neos.Neos:ContentCase) {
            @process.addItemAndActiveClass = Neos.Fusion:Augmenter {
                class = ${'item' + (iterator.isFirst ? ' active' : '')}
            }
        }
    }

    carouselItemNumber = ${q(node).children('carouselItems').children().count()}

    renderer = afx`
        <div>
            <div class="carousel slide" id={props.identifier} data-bs-ride="true">

                <div class="carousel-indicators">
                    <Neos.Fusion:Loop items={Array.range(1, props.carouselItemNumber)} itemName="index" iterationName="iterator">
                        <li
                            data-bs-target={'#' + props.identifier}
                            data-bs-slide-to={iterator.index}
                            class={iterator.isFirst ? 'active' : ''}
                        ></li>
                    </Neos.Fusion:Loop>
                </div>

                {props.carouselItems}

                <a class="carousel-control-prev" href={'#' + props.identifier} data-bs-slide="prev">
                    <span class="carousel-control-prev-icon" aria-hidden="true"></span>
                    <span class="visually-hidden">Previous</span>
                </a>
                <a class="carousel-control-next" href={'#' + props.identifier} data-bs-slide="next">
                    <span class="carousel-control-next-icon" aria-hidden="true"></span>
                    <span class="visually-hidden">Next</span>
                </a>
            </div>
        </div>
    `
}
